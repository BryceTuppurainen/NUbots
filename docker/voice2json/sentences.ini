# This file defines the grammar accepted by the speech to text engine.
# Syntax reference: http://voice2json.org/sentences.html

[Defs]
# This is not an actual intent. Instead it's just a collection of useful things.

# Rules
taxi    = (taxi | cab | uber){transport_type}

# Verbs

vbbtake   = bring | take
vbbring   = bring | give
vbdeliver = <vbbring> | deliver
vbfind    = find | locate | look for
vbguide   = guide | escort | take | lead | accompany

# A named or described person in the given place
whowhere = (the person){subject} ($gesture){gesture} in the ($room){room}

# People
someone  = (me){subject} | <whowhere>

# Object properties
oprop    = (biggest | largest | smallest | heaviest | lightest | thinnest){property}

# Navigating
vbgopl    = go | navigate
gobeacon = <vbgopl> to the ($beacon){subject_location}
goroom   = <vbgopl> to the ($room){target_location}


[FindObjects]

<Defs.vbfind> the ($object){object} <in_room>
<Defs.vbfind> the ($aobjectq){object} <in_room>
<Defs.vbfind> the ($category){category} <in_room>
# meta: Place only 3 objects in the room
<Defs.vbfind> three ($category){category} <in_room>
in_room = in the ($room){room}

Tell me how many ($object){object} there are <on_placement>
Tell me how many ($category){category} there are <on_placement>
Tell me whats the <Defs.oprop> object <on_placement>
Tell me whats the <Defs.oprop> ($category){category} <on_placement>
# meta: Place at least 5 objects on the placement
Tell me which are the three <Defs.oprop> (objects | ($category){category}) <on_placement>
on_placement = on the ($placement){placement}

[ComplexManipulation]
# meta: Block access to placement with a movable object or furniture
<Defs.vbbtake> the ($kobject){object} to the ($placement){placement}
<Defs.vbbring> me the <abspos> object <cmanobjsrc>
<Defs.vbbring> me the object <relpos> the ($object){object} <cmanobjsrc>
<Defs.vbbring> me the <Defs.oprop> (object | ($category){category}) <cmanobjsrc>
# meta: Place at least 5 objects in the {placement 2}
cmanobjsrc  = from the ($placement){placement}
abspos   = ((left | right) most){absolute_position}
relpos  = (at the (left | right) of | on top of | above | behind | under){relative_position}

[CleanUp]
# meta: Place 3 objects randomly, at least one on the floor.
(clean out | clean up | tidy up | neaten | order){action} the ($room){room}

[EmptyTrash]
(take out | dump) the (litter | garbage | trash | waste | debris | junk)

[Deliver]

<Defs.vbbtake> the ($aobject){object} from the ($room){subject_location} to the ($placement){target_location}
<Defs.vbbring> me the ($aobject){object} from the ($placement){subject_location}
<Defs.vbbtake> my <luggage> to the <Defs.taxi>
luggage = (bag | baggage | valise | suitcase | trolley){luggage_type}

<take> and <vbplace> it on the ($placement){target_location}
take     = (get | grasp | take | pick up) the ($object){object}

<takefrom> and (<delivme> | <delivat>)
<takefrom> and <place>
takefrom = <take> from the ($placement){target_location}
place    = <vbplace> it on the ($placement){target_location}

<vbplace> the <object> on the ($placement){target_location}
<Defs.vbbring> (me){subject} the <object>
<Defs.vbdeliver> the <object> to <Defs.someone>
<goplace> <Defs.vbfind> the <object> and (<delivme> | <delivat>)
<goplace> <Defs.vbfind> the <object> and <place>

goplace  = <Defs.vbgopl> to the ($placement){target_location}
delivme  = <Defs.vbdeliver> it to (me){subject}
delivat  = <Defs.vbdeliver> it to ($name){subject} at the ($beacon){subject_location}
object = ($object){object}
vbplace   = put | place

[FindPeople]
Tell me the <state> of the person at the ($beacon){subject_location}
Tell me the <state> of the person in the ($room){subject_location}
Tell me how many people in the ($room){subject_location} are (<pgenderp> | <pose>)

pgenderp = (men | women | boys | girls | male | female)
state = (name | gender | pose){state}
pose     = (sitting | standing | lying down){pose}

# fixme: these do not make sense.
<talk> to <Defs.whowhere>
talk     = <answer> | <speak>
answer   = answer a ($question){question}
speak    = (tell | say) <whattosay>

whattosay = something about yourself \
    | the time \
    | what day is (today | tomorrow){date_request} \
    | your teams (name | country | affiliation){team_request} \
    | the day of the (week | month){date_part_request} \
    | a joke

# Unused?
fndppl   = <findp> in the ($room) and <talk>
fndppl   = <Defs.goroom> <findp> and <talk>
findp    = <vbfind> a <pgenders> | <vbfind> a person ($gesture) | <vbfind> a person <pose>
pgenders = (man | woman | boy | girl | male person | female person)

[Follow] # done
#{void meta: Follow {name 1} to the {room 2}}
Follow ($name){subject} from the ($beacon){subject_location} <to_room>
Meet ($name){subject} at the ($beacon){subject_location} and follow ($pron) <to_room>
<Defs.gobeacon> meet ($name){subject} and follow ($pron) <to_room>
to_room = to the ($room){target_location}

[Meet] # done
Meet ($name){subject} at the ($beacon){subject_location} follow ($pron) and <Defs.goroom>
Meet ($name){subject} at the ($beacon){subject_location} follow ($pron) and <Defs.vbguide> ($pron) back

[IncompleteCommands] # done
# meta: {name 1} is at the {beacon 1}
Follow ($name){subject}

<Defs.vbbring> (me){subject} the ($object){object}
<Defs.vbdeliver> ($object){object} to <Defs.someone>
<Defs.vbguide> to the ($beacon){beacon}

# meta: {name 1} must be taken from {beacon 1} to {beacon 2}, but he will get lost before reaching it
Meet ($name){name} and <Defs.vbguide> ($pron)
# meta: {name 1} must be taken from {beacon 1} to {beacon 2}, but he will get lost before reaching it
<Defs.gobeacon> meet ($name){subject} and <Defs.vbguide> ($pron)

[PartyHost] # done
<vbserve> (drinks | snacks){type} to <people> in the ($room){room}
<vbmeet> ($name){name} at the <door>{door} and introduce ($pron) to <people> in the ($room){room}
<vbmeet> ($name){name} at the ($beacon){beacon} and ask ($pron) to leave
<vbmeet> ($name){new_guest} at the ($beacon){new_guest_beacon} and introduce ($pron) to ($name){existing_guest} at the ($beacon){existing_guest_beacon}
<vbmeet> ($name){name} at the ($beacon){beacon} and <Defs.vbguide>{guide} ($pron) to ($pron_pos) <Defs.taxi>

door        = (front | back | main | rear) (entrance | door)
vbserve     = serve | arrange | deliver | distribute | give | provide
vbmeet      = contact | face | find | greet
people      = (everyone){subject} \
            | all the (people | men | women | guests | elders | children){subject}

[Guide] # done
<Defs.vbguide> ($name){subject} from the ($beacon){subject_location} to the ($beacon){target_location}
Meet ($name){subject} at the ($beacon){subject_location} and <guideto>
<Defs.gobeacon> meet ($name){subject} and <guideto>
<Defs.vbguide> ($name){subject} to the ($beacon){target_location} <gdwhere>

guideto  = <Defs.vbguide> ($pron) to the ($beacon){target_location}
gdwhere  = you (may | can | will) find ($pron) at the ($beacon){target_location}
